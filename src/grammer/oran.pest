// values
integer = @{ ASCII_DIGIT+ }
decimal = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
escape_char = { "\\" }
escaped_escape_char = { "\\\\" }
escaped = { escape_char ~ ("'"|"\"") }
single_quote_base = @{ (!"'" ~ ANY)  }
double_quote_base = @{ (!"\"" ~ ANY)  }
single_quote_string = ${ "'" ~ ( escaped_escape_char | escaped | single_quote_base )* ~ "'" }
double_quote_string = ${ "\"" ~ ( escaped_escape_char | escaped | double_quote_base )* ~ "\"" }
string = { single_quote_string | double_quote_string }

// camparison operators
two_equals = { "==" }
bigger_than = { ">" }
smaller_than = { "<" }
e_bigger_than = { "=>" }
e_smaller_than = { "=<" }

// string related operators
concatenated_string = !{ possible_string ~ ("." ~ possible_string)+ }
possible_string = _{ string | ident | number | function_call | term }

// calc operators
plus = { "+" }
minus = { "-" }
times = { "*" }
divide = { "/" }
modulus = { "%" }
calc = _{ plus | minus | times | divide | modulus }

// variables prefix
var_let = { "let" }
var_const = { "const" }
var_prefix = { var_let | var_const }

// White spaces and comments
WHITESPACE = _{ " " | "\r" | "\n" | "\t" }
COMMENT = _{ (("//" ~ (!NEWLINE ~ ANY)*) | ("/*" ~ ( "/**/" | (!"*/" ~ ANY) )* ~ "*/")) }

// variable assignment
assgmt_expr = { var_prefix ~ ident ~ "=" ~ expr }
re_assgmt_expr = { ident ~ "=" ~ expr }

// term
term = {  primary ~ (calc ~ primary)* }
primary = { "(" ~ term ~ ")" | ident | number | function_call }
number = { decimal | integer }

// functions
function_name = ${ (ASCII_ALPHANUMERIC | "_")+ }
function_call = ${ function_name ~ WHITESPACE* ~ "(" ~ arguments ~ ")" }
function_define = ${ "fn" ~ (WHITESPACE|NEWLINE|COMMENT)* ~ function_name ~ (WHITESPACE|NEWLINE|COMMENT)* ~ "(" ~ arguments ~ ")"  ~ (WHITESPACE|NEWLINE|COMMENT)* ~ "{" ~ (WHITESPACE|NEWLINE|COMMENT)* ~ (stmt_in_function ~ (WHITESPACE|NEWLINE|COMMENT)*)* ~ last_stmt_in_function? ~ "}" }

// function arguments
arguments = { (operation ~ ("," ~ WHITESPACE* ~ operation)*)* }

// return
fn_return = { "return" ~ term* }

// deliminater
end_mark = _{ ";" }

// statements and expressions
stmt = _{ expr ~ NEWLINE* ~ end_mark+ | expr_without_end_mark }
stmt_in_function = _{ expr_in_function ~ NEWLINE* ~ end_mark+ }
last_stmt = _{ expr }
last_stmt_in_function = _{ last_stmt }
expr = {
    assgmt_expr
    | re_assgmt_expr
    | function_call
    | term
    | number
    | concatenated_string
    | string
}
expr_without_end_mark = {
    function_define
}
// expressions except assignment
operation = _{
    function_call
    | term
    | number
    | concatenated_string
    | string
}
expr_in_function = _{
    fn_return
    | assgmt_expr
    | re_assgmt_expr
    | function_call
    | term
    | number
    | concatenated_string
    | string
}

// program
program = _{ SOI ~ NEWLINE* ~ (stmt ~ NEWLINE*)* ~ last_stmt? ~ EOI }
