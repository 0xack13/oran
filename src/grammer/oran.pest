// values
escape_char = _{ "\\" }
escaped_escape_char = _{ "\\\\" }
integer = @{ ASCII_DIGIT+ }
decimal = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
single_quote_string = @{ "'" ~ (escape_char ~ "'" | (!"'" ~ ANY) )* ~ "'" }
double_quote_string = @{ "\"" ~ ( escaped_escape_char | escape_char ~ "\"" | (!"\"" ~ ANY) )* ~ "\"" }
string = { single_quote_string | double_quote_string }

// camparison operators
two_equals = { "==" }
bigger_than = { ">" }
smaller_than = { "<" }
e_bigger_than = { "=>" }
e_smaller_than = { "=<" }

// string related operators
concatenated_string = !{ possible_string ~ ("." ~ possible_string)+ }
possible_string = _{ string | ident | number | function_call | term }

// calc operators
plus = { "+" }
minus = { "-" }
times = { "*" }
divide = { "/" }
modulus = { "%" }
calc = _{ plus | minus | times | divide | modulus }

// variables prefix
var_let = { "let" }
var_const = { "const" }
var_prefix = { var_let | var_const }

// White spaces and comments
WHITESPACE = _{ " " | "\r" | "\n" | "\t" }
COMMENT = _{ (("//" ~ (!NEWLINE ~ ANY)*) | ("/*" ~ ( "/**/" | (!"*/" ~ ANY) )* ~ "*/")) }

// variable assignment
assgmt_expr = { var_prefix ~ ident ~ "=" ~ expr }
re_assgmt_expr = { ident ~ "=" ~ expr }

// term
term = {  primary ~ (calc ~ primary)* }
primary = { "(" ~ term ~ ")" | ident | number }
number = { decimal | integer }

// functions
function_name = ${ (ASCII_ALPHANUMERIC | "_")+ }
function_call = ${ function_name ~ "(" ~ opration ~ ")" | function_name ~ " " ~ "(" ~ opration ~ ")" }

end_one_execution = _{ ";" }

// statements and expressions
stmt = _{ expr }
expr = {
    assgmt_expr
    | re_assgmt_expr
    | function_call
    | term
    | number
    | concatenated_string
    | string
}
// expressions except assignment
opration = _{
    term
    | number
    | concatenated_string
    | string
    | function_call
}

program = _{ SOI ~ NEWLINE* ~ (stmt ~ NEWLINE* ~ end_one_execution+ ~ NEWLINE*) * ~ stmt? ~ EOI }
